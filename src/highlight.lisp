(in-package :cl-repl)

(defvar *exiting-p* nil "EXIT-WITH-PROMPT binds this to non-NIL while CL-REPL is exiting.")

(defun escape-name (name)
  (ppcre:regex-replace-all
    "\\+"
    (ppcre:regex-replace-all
      "\\*"
      name "\\\\*")
    "\\\\+"))

(defun list-regex (lst)
  (format nil
          "((?<=\\s)|^|(?<=\\()|(?<=\\)))(狳)(?=(\\s|\\(|\\)|$))"
          (sort lst #'string>)))

(defvar *syntax-table*)

(destructuring-bind (functions specials)
  (loop :for sym :being :the :external-symbols :of :cl
        :when (handler-case (symbol-function sym) (error () nil))
              :collect (escape-name (string-downcase sym)) :into functions
        :when (special-operator-p sym)
              :collect (escape-name (string-downcase sym)) :into specials
        :finally (return (list functions specials)))
  (setf *syntax-table*
    (list
     :magic (list *magic-syntax-color* "^%\\S+")
     :string (list *string-syntax-color* "\".*?\"")
     :variable (list *variable-syntax-color* "([\\*])\\S+\\1")
     :constant (list *constant-syntax-color* "([\\+])\\S+\\1")
     :keyword (list *keyword-syntax-color* "((?<=\\s)|^):\\S+(?=\\b)")
     :definition (list *definition-syntax-color*
                       "((?<=defun)|(?<=defmacro)|(?<=defmethod)|(?<=defgeneric))\\s\\S+(?=\\b)")
     :lambda (list *lambda-syntax-color*
                   (list-regex '("&allow-other-keys" "&aux" "&body" "&environment" "&key" "&optional" "&rest" "&whole")))
     :special (list *special-syntax-color* (list-regex specials))
     :function (list  *function-syntax-color* (list-regex functions))
     :boolean (list *boolean-syntax-color* (list-regex '("nil" "t")))
     :normal (list *normal-syntax-color* "."))))

(defun map-syntax (syntax text &optional syntax-map)
  (unless syntax-map
    (setf syntax-map (make-list (length text) :initial-element nil)))
  (destructuring-bind (color regex) (getf *syntax-table* syntax)
    (ppcre:do-matches (start end regex text)
      (loop :for n :from start :below end
            :unless (elt syntax-map n)
            :do (setf (elt syntax-map n)
                      (color color (elt text n)
                             :prompt-chars nil)))))
  syntax-map)

(defun highlight-text (text)
  (let ((syntax-map))
    (loop :for (syntax val) :on *syntax-table* :by #'cddr
          :do (setf syntax-map (map-syntax syntax text syntax-map)))
    (format nil "狺祜镳烘矧蜥横泸矬翦烘矧泔祜蝈洪簌铘狲磲恒镬戾泗矧泔祜蝈蜥鳗┅┅ㄤ彐磲泸鏖翳沲蝮矧轭骘蝽狒轱è扉铄鲠泔飙鲠颟怙澌怙澌戾è痱镯痿ㄧ孱簌⑿蚁托寓┅扉铄ㄧ孱簌⑻晌庞┅痫轭ㄧ孱簌⑿仙卧┅痱弼轱躞铄黛轭瀛痫箝糸镱ㄧ孱簌⑿遗稚险迎闻滋晌怒邢由陨衔┅啜戾舄è痱镯痿痱镯痿篝蜷铉┅ì扉铄ㄣ镱汜翦钺翦篝蜷铉痱镯痿蜢邯扉铄怩骀弪┅ì痫轭ǐ戾铉翳痱镯痿蜢邯痫轭舄┅ì扉铄鲠ㄣ秕铘＼铄黛轭扉铄哄钿痫轭舂ì泔飙鲠戾è痱弼轱躞铄黛轭瀛痫箝糸镱矧痫箝糸镱＼铄黛轭扉铄哄钿痫轭烘蝻憝孱舂癌┅ō痫轭痱弼轱躞铄黛轭瀛痫箝糸镱ㄩ弪镳扉铄鲠颟换溟筱狎翳铄黛轭暴┅┅ㄤ邈灬蝈ㄩ珙矧徕戾扉铄鲠泔飙鲠颟棱镤┅ㄤ彐躅盹鲥沲蝮矧骝镯痫轭舡麸痱镯痿篝狎ī鏖翳沲蝮矧轭骘蝽狒轱扉铄铛礅弪泔飙铛礅弪躅戾篌弪镳扉铄铛礅弪ㄦ矧磲阚崞换盹鲥沲蝮矧扉铄躔麸翳忮玳铑轭镦扉铄＼弩扉铄铛礅弪┅┅ㄤ彐躅盹鲥沲蝮矧骝镯痱镯痿篝狎舡麸痫轭ī鏖翳沲蝮矧轭骘蝽狒轱扉铄铛礅弪泔飙铛礅弪躅戾篌弪镳扉铄铛礅弪ㄦ矧磲阚崤换盹鲥沲蝮矧扉铄滹黝麸翳忮玳铑轭镦扉铄＼弩扉铄铛礅弪┅躅戾篌弪镳泔飙铛礅弪ㄦ矧磲阚扒阚崦换泔祯眍麸翳蜷玷＼弩＼弩泔飙铛礅弪┅┅ㄤ彐躅蝈溟箴灬鏖翳栝玷扉玷ī换澡轶骢钽糸镱轶汜祆邃麒孱翳沲蝮矧轶盹鲥洮矧犷翦轶躔溽翦洚蜢候邃轶痨狴戾舄è扉铄蜢邯扉铄怩骀弪ㄨ飙扉铄ㄨ殓桁殓梏翦扉铄螬┅麒孱矧屮轸轭绛皙弪镳戾铉翳扉铄螬┅蝈趱蝾骝镯蝈溟箴灬鏖翳栝玷扉玷舂祜镳洪铋糸犰禊盹鲥沲蝮矧骝镯痫轭舡麸痱镯痿篝狎舂烘矧扉铄铛烘蝻瑚轸铎痫航瑚轸镬洵铎痫航铋瑚栝戾铎痫轰箦赳镬洵铎痫ū铎痫螬箦赳铎痫痫箝糸镱＼铄黛轭桁扉铄后翎螋ū铎痫螬┅戾è扉铄篚怏羼桁扉铄镬洵铎痫铎痫螬┅换腻戾翦翳屮轶糸铉扉铄翳孱黩轸翳栝玷扉玷翦扉铄换义驽蝈钽搴梏麴蠛玳篝玳翳踱泔懑骖膣吹阜惫炒翅徕浒便驸狈岢岽娣补斗狗换洛祜骱换弩汜疱菜溴戾翦翳孱糸蝈扉铄换弩汜疱Ｄ盹鲥沲蝮矧戾骠泔祯眍换弩汜疱Ｃ盹鲥沲蝮矧蜷玷泔祯眍ㄩ弪镳扉铄铛愆ㄦ矧磲阚菜阚扒狺幄＼弩＼弩蜢邯溟箴灬痱镯痿扉铄ㄦ矧磲阚菜阚扒幄＼弩＼弩扉铄┅麒孱铎痫ㄦ矧磲阚迸＼弩悌盹鲥沲蝮矧滹黝扉铄烘轭犰禊戾è铎泔躅ㄣ秕铘＼铄黛轭扉铄螬┅ㄩ弪镳铎泔躅舂ㄦ矧磲阚扒阚崦换泔祯眍麸翳蜷玷＼弩＼弩戾铉翳痱镯痿篝蜷铉┅ㄦ矧磲阚崞阚崦换盹鲥沲蝮矧扉铄躔换犷泔祯眍麸翳蜷玷＼弩铎泔躅＼弩戾铉翳痱镯痿篝蜷铉┅┅盹鲥沲蝮矧骝镯痱镯痿篝狎舡麸痫轭舂蜢候邃轶痨狴┅ㄦ轭轶璀秕麴豸┅ㄤ彐鲠簌铘狲孱徕戾洫铋飑ㄤ彐躅孱徕戾簌铘狲ī箦翩簌铘狲孱徕戾洫舂换遗纳有塘骢钽糸镱轶犰箫汜祆邃麒孱翳沲蝮矧轶盹鲥洚蜢候彗轶翦颦骢钽糸镱候邃轶痨狴＇蝈溟箴灬鏖翳栝玷扉玷舂ㄤ彐躅溟筢忪瀛簌铘狲ī箦翩簌铘狲孱徕戾洫铋飑蜢候彗轶翦颦骢钽糸镱候邃轶痨狴＇蜢候邃轶痨狴┅